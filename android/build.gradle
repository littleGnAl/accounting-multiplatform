buildscript {
    ext.versions = [
        'kotlin': '1.4.20',
        'coroutines': '1.4.2',
        'sqldelight': '1.4.3',
        'serialiaztion': '1.0.1'
    ]

    ext.deps = [
        'ktlint': "com.pinterest:ktlint:0.34.2",
        'kotlin': [
            'stdlib': [
                'stdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
                'jdk7': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
            ],
            'serialiaztion': "org.jetbrains.kotlinx:kotlinx-serialization-json:${versions.serialiaztion}",
            'coroutines': [
                'core': "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}",
                'android': "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}",
            ],
        ],
        'sqldelight': [
            'gradleplugin': "com.squareup.sqldelight:gradle-plugin:${versions.sqldelight}",
            'runtimejvm': "com.squareup.sqldelight:runtime-jvm:${versions.sqldelight}",
            'driver': [
                'native': "com.squareup.sqldelight:native-driver:${versions.sqldelight}",
                'android': "com.squareup.sqldelight:android-driver:${versions.sqldelight}",
            ],
        ],
    ]



    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
        classpath deps.sqldelight.gradleplugin
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    apply from: "${project.rootDir}/ktlint.gradle"

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('ktlint')
        }
    }
}

/**
 * Get gradle wrapper version and pass it to ci/gradle_cache_cleanup.sh
 */
task getGradleWrapperVersion() {
    doLast {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('gradle/wrapper/gradle-wrapper.properties')
            .newDataInputStream())
        def distributionUrl = properties.getProperty('distributionUrl')
        def versionNumMatcher = (distributionUrl =~ /[0-9]\.[0-9]\.?[0-9]?/)
        println versionNumMatcher[0]
    }
}
