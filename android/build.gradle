buildscript {
    ext.versions = [
        'kotlin': '1.3.50',
        'coroutines': '1.3.1',
        'sqldelight': '1.2.0',
        'serialiaztion': '0.12.0'
    ]

    ext.deps = [
        'ktlint': "com.pinterest:ktlint:0.34.2",
        'kotlin': [
            'stdlib': [
                'stdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
                'jdk7': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
            ],
            'serialiaztion': [
                'runtime': [
                    'runtime': "org.jetbrains.kotlinx:kotlinx-serialization-runtime:${versions.serialiaztion}",
                    'common': "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:${versions.serialiaztion}",
                    'native': "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:${versions.serialiaztion}"
                ],
            ],
            'coroutines': [
                'common': "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:${versions.coroutines}",
                'android': "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}",
                'native': "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:${versions.coroutines}",
            ],
        ],
        'sqldelight': [
            'gradleplugin': "com.squareup.sqldelight:gradle-plugin:${versions.sqldelight}",
            'runtimejvm': "com.squareup.sqldelight:runtime-jvm:${versions.sqldelight}",
            'driver': [
                'ios': "com.squareup.sqldelight:ios-driver:${versions.sqldelight}",
                'android': "com.squareup.sqldelight:android-driver:${versions.sqldelight}",
            ],
        ],
    ]



    repositories {
        google()
        mavenCentral()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
        classpath deps.sqldelight.gradleplugin
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        jcenter()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    apply from: "${project.rootDir}/ktlint.gradle"

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('ktlint')
        }
    }
}

/**
 * Get gradle wrapper version and pass it to ci/gradle_cache_cleanup.sh
 */
task getGradleWrapperVersion() {
    doLast {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('gradle/wrapper/gradle-wrapper.properties')
            .newDataInputStream())
        def distributionUrl = properties.getProperty('distributionUrl')
        def versionNumMatcher = (distributionUrl =~ /[0-9]\.[0-9]\.?[0-9]?/)
        println versionNumMatcher[0]
    }
}
